{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Hyperelasticity\n",
    "\n",
    "**Keywords**: *hyperelasticity*, *finite strain*, *large deformations*, *Newton's method*,\n",
    "*conjugate gradient*, *automatic differentiation*\n",
    "\n",
    "![hyperelasticity.png](hyperelasticity.png)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Introduction\n",
    "\n",
    "In this example we will solve a problem in a finite strain setting using an\n",
    "hyperelastic material model. In order to compute the stress we will use automatic\n",
    "differentiation, to solve the non-linear system we use Newton's\n",
    "method, and for solving the Newton increment we use conjugate gradient.\n",
    "\n",
    "The weak format is expressed in terms of the first Piola-Kirchoff stress \\$\\mathbf{P}\\$\n",
    "as follows: Find \\$u \\in \\mathbb{U}\\$ such that\n",
    "\n",
    "$$\n",
    " \\int_\\Omega [\\delta \\mathbf{u} \\otimes \\nabla] : \\mathbf{P}(\\mathbf{u})\\ \\mathrm{d}\\Omega =\n",
    "\\int_\\Omega \\delta \\mathbf{u} \\cdot \\mathbf{b}\\ \\mathrm{d}\\Omega + \\int_{\\Gamma^\\mathrm{N}}\n",
    "\\mathbf{u} \\cdot \\mathbf{t}\\ \\mathrm{d}\\Gamma\n",
    "\\quad \\forall \\delta \\mathbf{u} \\in \\mathbb{U}^0,\n",
    "$$\n",
    "\n",
    "where \\$\\mathbf{u}\\$ is the unknown displacement field, \\$\\mathbf{b}\\$ is the body force, \\$\\mathbf{t}\\$\n",
    "is the traction on the Neumann part of the boundary, and where \\$\\mathbb{U}\\$ and \\$\\mathbb{U}^0\\$ are\n",
    "suitable trial and test sets."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuAFEM, Tensors, TimerOutputs, ProgressMeter\n",
    "import KrylovMethods, IterativeSolvers"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Hyperelastic material model\n",
    "\n",
    "The stress can be derived from an energy potential, defined in\n",
    "terms of the right Cauchy-Green tensor \\$\\mathbf{C}\\$. We shall use a neo-Hookean model, where\n",
    "the potential can be written as\n",
    "\n",
    "$$\n",
    "\\Psi(\\mathbf{C}) = \\frac{\\mu}{2} (I_C - 3) - \\mu \\ln(J) + \\frac{\\lambda}{2} \\ln(J)^2,\n",
    "$$\n",
    "\n",
    "where \\$I_C = \\mathrm{tr}(C)\\$, \\$J = \\sqrt{\\det(C)}\\$ and \\$\\mu\\$ and \\$\\lambda\\$ material parameters.\n",
    "From the potential we obtain the second Piola-Kirchoff stress \\$\\mathbf{S}\\$ as\n",
    "\n",
    "$$\n",
    "\\mathbf{S} = 2 \\frac{\\partial \\Psi}{\\partial \\mathbf{C}},\n",
    "$$\n",
    "\n",
    "and the tangent of \\$\\mathbf{S}\\$ as\n",
    "\n",
    "$$\n",
    "\\frac{\\partial \\mathbf{S}}{\\partial \\mathbf{C}} = 4 \\frac{\\partial \\Psi}{\\partial \\mathbf{C}}.\n",
    "$$\n",
    "\n",
    "We can implement the material model as follows, where we utilize automatic differentiation\n",
    "for the stress and the tangent, and thus only define the potential:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct NeoHooke\n",
    "    μ::Float64\n",
    "    λ::Float64\n",
    "end\n",
    "\n",
    "function Ψ(C, mp::NeoHooke)\n",
    "    μ = mp.μ\n",
    "    λ = mp.λ\n",
    "    Ic = tr(C)\n",
    "    J = sqrt(det(C))\n",
    "    return μ / 2 * (Ic - 3) - μ * log(J) + λ / 2 * log(J)^2\n",
    "end\n",
    "\n",
    "function constitutive_driver(C, mp::NeoHooke)\n",
    "    # Compute all derivatives in one function call\n",
    "    ∂²Ψ∂C², ∂Ψ∂C = Tensors.hessian(y -> Ψ(y, mp), C, :all)\n",
    "    S = 2.0 * ∂Ψ∂C\n",
    "    ∂S∂C = 4.0 * ∂²Ψ∂C²\n",
    "    return S, ∂S∂C\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Finally, for the finite element problem we need \\$\\mathbf{P}\\$ and\n",
    "\\$\\frac{\\partial \\mathbf{P}}{\\partial \\mathbf{F}}\\$, which can be obtained by the following\n",
    "transformations\n",
    "\n",
    "$$\n",
    "\\begin{align*}\n",
    "\\mathbf{P} &= \\mathbf{F} \\cdot \\mathbf{S},\\\\\n",
    "\\frac{\\partial \\mathbf{P}}{\\partial \\mathbf{F}} &= [\\mathbf{F} \\bar{\\otimes} \\mathbf{I}] :\n",
    "\\frac{\\partial \\mathbf{S}}{\\partial \\mathbf{C}} : [\\mathbf{F}^\\mathrm{T} \\bar{\\otimes} \\mathbf{I}]\n",
    "+ \\mathbf{I} \\bar{\\otimes} \\mathbf{S}.\n",
    "\\end{align*}\n",
    "$$\n",
    "\n",
    "## Finite element assembly\n",
    "\n",
    "The element routine for assembling the residual and tangent stiffness is implemented\n",
    "as usual, with loops over quadrature points and shape functions:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "function assemble_element!(ke, ge, cell, cv, fv, mp, ue)\n",
    "    # Reinitialize cell values, and reset output arrays\n",
    "    reinit!(cv, cell)\n",
    "    fill!(ke, 0.0)\n",
    "    fill!(ge, 0.0)\n",
    "\n",
    "    b = Vec{3}((0.0, -0.5, 0.0)) # Body force\n",
    "    t = Vec{3}((0.1, 0.0, 0.0)) # Traction\n",
    "    ndofs = getnbasefunctions(cv)\n",
    "\n",
    "    for qp in 1:getnquadpoints(cv)\n",
    "        dΩ = getdetJdV(cv, qp)\n",
    "        # Compute deformation gradient F and right Cauchy-Green tensor C\n",
    "        ∇u = function_gradient(cv, qp, ue)\n",
    "        F = one(∇u) + ∇u\n",
    "        C = tdot(F)\n",
    "        # Compute stress and tangent\n",
    "        S, ∂S∂C = constitutive_driver(C, mp)\n",
    "        P = F ⋅ S\n",
    "        I = one(S)\n",
    "        ∂P∂F = otimesu(F, I) ⊡ ∂S∂C ⊡ otimesu(F', I) + otimesu(I, S)\n",
    "\n",
    "        # Loop over test functions\n",
    "        for i in 1:ndofs\n",
    "            # Test function + gradient\n",
    "            δui = shape_value(cv, qp, i)\n",
    "            ∇δui = shape_gradient(cv, qp, i)\n",
    "            # Add contribution to the residual from this test function\n",
    "            ge[i] += ( ∇δui ⊡ P - δui ⋅ b ) * dΩ\n",
    "\n",
    "            ∇δui∂P∂F = ∇δui ⊡ ∂P∂F # Hoisted computation\n",
    "            for j in 1:ndofs\n",
    "                ∇δuj = shape_gradient(cv, qp, j)\n",
    "                # Add contribution to the tangent\n",
    "                ke[i, j] += ( ∇δui∂P∂F ⊡ ∇δuj ) * dΩ\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Surface integral for the traction\n",
    "    for face in 1:nfaces(cell)\n",
    "        if onboundary(cell, face)\n",
    "            reinit!(fv, cell, face)\n",
    "            for q_point in 1:getnquadpoints(fv)\n",
    "                dΓ = getdetJdV(fv, q_point)\n",
    "                for i in 1:ndofs\n",
    "                    δui = shape_value(fv, q_point, i)\n",
    "                    ge[i] -= (δui ⋅ t) * dΓ\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Assembling global residual and tangent"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    "function assemble_global!(K, f, dh, cv, fv, mp, u)\n",
    "    n = ndofs_per_cell(dh)\n",
    "    ke = zeros(n, n)\n",
    "    ge = zeros(n)\n",
    "\n",
    "    # start_assemble resets K and f\n",
    "    assembler = start_assemble(K, f)\n",
    "\n",
    "    # Loop over all cells in the grid\n",
    "    @timeit \"assemble\" for cell in CellIterator(dh)\n",
    "        global_dofs = celldofs(cell)\n",
    "        ue = u[global_dofs] # element dofs\n",
    "        @timeit \"element assemble\" assemble_element!(ke, ge, cell, cv, fv, mp, ue)\n",
    "        assemble!(assembler, global_dofs, ge, ke)\n",
    "    end\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Define a main function, with a loop for Newton iterations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "solve (generic function with 1 method)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function solve()\n",
    "    reset_timer!()\n",
    "\n",
    "    # Generate a grid\n",
    "    N = 10\n",
    "    L = 1.0\n",
    "    left = zero(Vec{3})\n",
    "    right = L * ones(Vec{3})\n",
    "    grid = generate_grid(Tetrahedron, (N, N, N), left, right)\n",
    "\n",
    "    # Material parameters\n",
    "    E = 10.0\n",
    "    ν = 0.3\n",
    "    μ = E / (2(1 + ν))\n",
    "    λ = (E * ν) / ((1 + ν) * (1 - 2ν))\n",
    "    mp = NeoHooke(μ, λ)\n",
    "\n",
    "    # Finite element base\n",
    "    ip = Lagrange{3, RefTetrahedron, 1}()\n",
    "    qr = QuadratureRule{3, RefTetrahedron}(1)\n",
    "    qr_face = QuadratureRule{2, RefTetrahedron}(1)\n",
    "    cv = CellVectorValues(qr, ip)\n",
    "    fv = FaceVectorValues(qr_face, ip)\n",
    "\n",
    "    # DofHandler\n",
    "    dh = DofHandler(grid)\n",
    "    push!(dh, :u, 3) # Add a displacement field\n",
    "    close!(dh)\n",
    "\n",
    "    function rotation(X, t, θ = deg2rad(60.0))\n",
    "        x, y, z = X\n",
    "        return t * Vec{3}(\n",
    "            (0.0,\n",
    "            L/2 - y + (y-L/2)*cos(θ) - (z-L/2)*sin(θ),\n",
    "            L/2 - z + (y-L/2)*sin(θ) + (z-L/2)*cos(θ)\n",
    "            ))\n",
    "    end\n",
    "\n",
    "    dbcs = ConstraintHandler(dh)\n",
    "    # Add a homogenous boundary condition on the \"clamped\" edge\n",
    "    dbc = Dirichlet(:u, getfaceset(grid, \"right\"), (x,t) -> [0.0, 0.0, 0.0], [1, 2, 3])\n",
    "    add!(dbcs, dbc)\n",
    "    dbc = Dirichlet(:u, getfaceset(grid, \"left\"), (x,t) -> rotation(x, t), [1, 2, 3])\n",
    "    add!(dbcs, dbc)\n",
    "    close!(dbcs)\n",
    "    t = 0.5\n",
    "    JuAFEM.update!(dbcs, t)\n",
    "\n",
    "    # Pre-allocation of vectors for the solution and Newton increments\n",
    "    _ndofs = ndofs(dh)\n",
    "    un = zeros(_ndofs) # previous solution vector\n",
    "    u  = zeros(_ndofs)\n",
    "    Δu = zeros(_ndofs)\n",
    "    ΔΔu = zeros(_ndofs)\n",
    "    apply!(un, dbcs)\n",
    "\n",
    "    # Create sparse matrix and residual vector\n",
    "    K = create_sparsity_pattern(dh)\n",
    "    g = zeros(_ndofs)\n",
    "\n",
    "\n",
    "    # Perform Newton iterations\n",
    "    newton_itr = -1\n",
    "    NEWTON_TOL = 1e-8\n",
    "    prog = ProgressMeter.ProgressThresh(NEWTON_TOL, \"Solving:\")\n",
    "\n",
    "    while true; newton_itr += 1\n",
    "        u .= un .+ Δu # Current guess\n",
    "        assemble_global!(K, g, dh, cv, fv, mp, u)\n",
    "        normg = norm(g[JuAFEM.free_dofs(dbcs)])\n",
    "        apply_zero!(K, g, dbcs)\n",
    "        ProgressMeter.update!(prog, normg; showvalues = [(:iter, newton_itr)])\n",
    "\n",
    "        if normg < NEWTON_TOL\n",
    "            break\n",
    "        elseif newton_itr > 30\n",
    "            error(\"Reached maximum Newton iterations, aborting\")\n",
    "        end\n",
    "\n",
    "        # Compute increment using cg! from IterativeSolvers.jl\n",
    "        @timeit \"linear solve (KrylovMethods.cg)\" ΔΔu′, flag, relres, iter, resvec = KrylovMethods.cg(K, g; maxIter = 1000)\n",
    "        @assert flag == 0\n",
    "        @timeit \"linear solve (IterativeSolvers.cg!)\" IterativeSolvers.cg!(ΔΔu, K, g; maxiter=1000)\n",
    "\n",
    "        apply_zero!(ΔΔu, dbcs)\n",
    "        Δu .-= ΔΔu\n",
    "    end\n",
    "\n",
    "    # Save the solution\n",
    "    @timeit \"export\" begin\n",
    "        vtk_grid(\"hyperelasticity\", dh) do vtkfile\n",
    "            vtk_point_data(vtkfile, dh, u)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    print_timer(title = \"Analysis with $(getncells(grid)) elements\", linechars = :ascii)\n",
    "    return u\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Run the simulation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Solving: (thresh = 1e-08, value = 0.194526)\u001b[K\n",
      "\u001b[Ater:  1\n",
      "Solving: (thresh = 1e-08, value = 1.92988e-07)\u001b[K\n",
      "\u001b[Ater:  4\n",
      "Solving: Time: 0:00:00 (6 iterations)\u001b[K\n",
      "  iter:  5\n",
      " ------------------------------------------------------------------------------\n",
      "  Analysis with 6000 elements          Time                   Allocations      \n",
      "                               ----------------------   -----------------------\n",
      "       Tot / % measured:            2.62s / 28.1%            193MiB / 11.9%    \n",
      "\n",
      " Section               ncalls     time   %tot     avg     alloc   %tot      avg\n",
      " ------------------------------------------------------------------------------\n",
      " export                     1    379ms  51.4%   379ms   13.6MiB  58.9%  13.6MiB\n",
      " assemble                   6    159ms  21.6%  26.6ms   8.25MiB  35.8%  1.37MiB\n",
      "   element assemble     36.0k    111ms  15.1%  3.08μs     0.00B  0.00%    0.00B\n",
      " linear solve (Iter...      5    158ms  21.5%  31.7ms    595KiB  2.52%   119KiB\n",
      " linear solve (Kryl...      5   40.2ms  5.45%  8.04ms    669KiB  2.83%   134KiB\n",
      " ------------------------------------------------------------------------------"
     ]
    }
   ],
   "source": [
    "u = solve();"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.3.1",
   "language": "julia",
   "name": "julia-1.3"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.3.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
